buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'org.ajoberstar:grgit:1.4.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}

dependencies {
    compile 'joda-time:joda-time:2.9.7'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.android.support:appcompat-v7:25.3.0'
}

android {

    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        versionName '1.10.0' + getVersionNameSuffix()
        versionCode getVersionCodeInteger()
        minSdkVersion 15
        targetSdkVersion 25

        testApplicationId "com.plusonelabs.calendar.tests"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            lintOptions {
                warning 'MissingTranslation','InvalidPackage'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    if (project.hasProperty('calendarStoreFile')) {
        signingConfigs {
            releaseConfig {
                storeFile file(calendarStoreFile)
                storePassword calendarStorePassword
                keyAlias calendarKeyAlias
                keyPassword calendarKeyPassword
            }
        }
        buildTypes.release.signingConfig signingConfigs.releaseConfig
    }
    // See https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
    if (project.hasProperty("calendar-widget.signing")
            && new File(project.property("calendar-widget.signing").toString() + ".gradle").exists()) {
        apply from: project.property("calendar-widget.signing") + ".gradle";
    }
}

String getVersionNameSuffix() {
    try {
        def git = org.ajoberstar.grgit.Grgit.open(dir: '../..')
        def versionNameSuffix = "-${git.head().abbreviatedId}"
        git.close()
        return versionNameSuffix
    } catch (ignored) {
        return ""
    }
}

def getVersionCodeInteger() {
    try {
        def git = org.ajoberstar.grgit.Grgit.open(dir: '../..')
        def versionCode = git.log().size()
        git.close()
        return versionCode
    } catch (ignored) {
        return 1
    }
}
